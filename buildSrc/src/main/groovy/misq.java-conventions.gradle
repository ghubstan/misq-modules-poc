// Build configurations shared by all sub-projects.
// We use the groovy-gradle-plugin (convention plugin) because the
// java convention plugin requires adding java code to the build.
plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    // id 'extra-java-module-info' // Apply custom plugin written here in buildSrc.
}

version = '0.0.1-SNAPSHOT'
group = 'com.github.ghubstan'

ext {
    createTestReports = false

    // Default generated src dirs:
    generatedGrpcProtoSrcDir = 'build/generated/source/main/grpc'
    generatedProtoSrcDir = 'build/generated/source/main/java'

    // Version properties in alphabetical order:
    checkerFrameworkVersion = '3.8.0'
    errorProneAnnotationsVersion = '2.5.1'
    grpcVersion = '1.37.0'
    gsonVersion = '2.8.6'
    guavaVersion = '30.1.1-jre'
    guiceVersion = '5.0.1'
    javaxAnnotationVersion = '1.3.2'
    jetbrainsAnnotationsVersion = '13.0'
    jsonrpc4jVersion = '1.6.0.bisq.1'
    jupiterVersion = '5.7.1'
    logbackVersion = '1.3.0-alpha5'  // force to upgrade to modularized logback
    lombokVersion = '1.18.20'
    protobufGradlePluginVersion = '0.8.16'
    protobufJavaVersion = '3.12.4'
    protocVersion = '3.12.4'
    slf4jVersion = '2.0.0-alpha1'   // force to upgrade to modularized logback
}

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

dependencies {
    annotationProcessor "org.projectlombok:lombok:$lombokVersion"
    testImplementation "org.junit.jupiter:junit-jupiter-api:$jupiterVersion"
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
}

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

java {
    withJavadocJar()
    withSourcesJar()
}

tasks.named('compileJava') {
    sourceCompatibility = 15
    targetCompatibility = 15
    modularity.inferModulePath = true
    options.javaModuleVersion = provider { project.version }
    options.compilerArgs += ['-Xdoclint:none', '-Xlint:none', '-nowarn']
}

tasks.named("javadoc") {
    source = sourceSets.main.allJava
    options.addStringOption('Xdoclint:none', '-quiet')
}

tasks.named("test") {
    useJUnitPlatform()
}

tasks.withType(Test).configureEach {
    maxParallelForks = Runtime.runtime.availableProcessors().intdiv(2) ?: 1
    if (!project.hasProperty("createTestReports")) {
        reports.html.enabled = false
        reports.junitXml.enabled = false
    }
}

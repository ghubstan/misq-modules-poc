// Build configurations shared by all sub-projects.
// We use the groovy-gradle-plugin because the java convention plugin requires add java code to the build.

plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

version = '0.0.1-SNAPSHOT'
group = 'com.github.ghubstan'

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

java {
    withJavadocJar()
    withSourcesJar()
}

tasks.named('compileJava') {
    sourceCompatibility = 15
    targetCompatibility = 15
    modularity.inferModulePath = true
    options.javaModuleVersion = provider { project.version }
    options.compilerArgs += ['-Xdoclint:none', '-Xlint:none', '-nowarn']
}

tasks.named("javadoc") {
    source = sourceSets.main.allJava
    options.addStringOption('Xdoclint:none', '-quiet')
}

tasks.named("test") {
    useJUnitPlatform()
}

configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}


ext { // in alphabetical order
    guavaVersion = '30.1.1-jre'
    jacksonVersion = '2.12.1'
    javaxAnnotationVersion = '1.3.2'
    jetbrainsAnnotationsVersion = '13.0'
    jsonrpc4jVersion = '1.6.0.bisq.1'
    jupiterVersion = '5.7.1'
    logbackVersion = '1.3.0-alpha5'  // force to upgrade to modularized logback
    lombokVersion = '1.18.20'
    slf4jVersion = '2.0.0-alpha1'   // force to upgrade to modularized logback
}


dependencies {
    implementation "com.google.errorprone:error_prone_annotations:2.5.1"
    implementation ("com.google.guava:guava:$guavaVersion") {
        exclude(module: 'failureaccess')
        exclude(module: 'findbugs')
        exclude(module: 'listenablefuture')
        exclude(module: 'jsr305')
        exclude(module: 'j2objc-annotations')
    }

    implementation "org.checkerframework:checker-qual:3.13.0"
    implementation "org.slf4j:slf4j-api:$slf4jVersion"
    implementation ("ch.qos.logback:logback-core:$logbackVersion")  {
        exclude(module: 'checker-framework')
    }
    implementation ("ch.qos.logback:logback-classic:$logbackVersion") {
        exclude(module: 'checker-framework')
        exclude(module: 'javax.mail')
    }

    compileOnly "javax.annotation:javax.annotation-api:$javaxAnnotationVersion"
    compileOnly "org.projectlombok:lombok:$lombokVersion"
    annotationProcessor "org.projectlombok:lombok:$lombokVersion"

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
}


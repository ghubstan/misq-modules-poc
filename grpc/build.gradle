plugins {
    id 'misq.java-conventions'
    id 'com.google.protobuf' version '0.8.16'
}

dependencies {
    implementation project(':annotation')
    implementation project(':logging')
    implementation project(':utilities')
    implementation project(':threadmodel')
    implementation project(':monetary')
    implementation project(':api')

    implementation "javax.inject:javax.inject:1"
    implementation "com.google.code.gson:gson:$gsonVersion"

    implementation "io.grpc:grpc-all:$grpcVersion"
    implementation "io.grpc:grpc-protobuf:$grpcVersion"
    runtimeOnly "io.grpc:grpc-netty-shaded:$grpcVersion"
}

sourceSets.main {
    java.srcDirs += [
            'build/generated/source/main/grpc',
            'build/generated/source/main/java'
    ]
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:$protocVersion"
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:$grpcVersion"
        }
    }
    generateProtoTasks {
        all()*.plugins { grpc {} }
    }
    generatedFilesBaseDir = "$projectDir/build/generated/source"
}

jar {
    // Exclude .proto files from generated jar.
    exclude '*.proto'
    includeEmptyDirs false
}

tasks.named("sourcesJar") {
    // The 'dependsOn generateProto' configuration avoids gradle warning:  Task ':proto:sourcesJar' uses
    // this output of task ':proto:generateProto' without declaring an explicit or implicit dependency.
    dependsOn generateProto
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = project.getGroup()
            artifactId = project.getName()
            version = project.getVersion()
            from components.java
        }
    }
    // Publish to jitpack:         $ ./gradlew clean build assemble publish
    // Install in local mvn repo:  $ ./gradlew clean build publishToMavenLocal
    //
    // To import individual module-playground modules:
    //    implementation 'com.github.ghubstan.module-playground:cli:<commit>'
    //    implementation 'com.github.ghubstan.module-playground:fisq:<commit>'
    // This does import the transitive dependencies.
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/ghubstan/" + project.getName())
            credentials {
                username = "ghubstan"
                password = System.getProperty("ghubtoken")
            }
        }
    }
}

signing {
    // See https://docs.gradle.org/current/userguide/signing_plugin.html#signing_plugin
    // The Signing Plugin currently only provides support for generating OpenPGP signatures
    // (which is the signature format required for publication to the Maven Central Repository).
    //
    // sign publishing.publications.maven
    // Cannot perform signing task ':cli:signMavenPublication' because it has no configured signatory
}

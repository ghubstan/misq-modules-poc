// https://github.com/google/protobuf-gradle-plugin

plugins {
    id 'misq.java-conventions'
    id 'com.google.protobuf' version '0.8.16'
}

dependencies {
    implementation "com.google.protobuf:protobuf-gradle-plugin:0.8.16"

    implementation "io.grpc:grpc-all:1.37.0"
    implementation "io.grpc:grpc-protobuf:1.37.0"

    // TODO see if this is all need (not all)
    // protobuf-java, grpc-stub, grpc-protobuf, and grpc-core

    api "javax.annotation:javax.annotation-api:$javaxAnnotationVersion"
    api "org.projectlombok:lombok:$lombokVersion"
}

sourceSets.main {
    java.srcDirs += [
            'build/generated/source/main/grpc',
            'build/generated/source/main/java'
    ]
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.12.4"
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:1.37.0"
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
    generatedFilesBaseDir = "$projectDir/build/generated/source"
}

jar {
    // Exclude .proto files from generated jar.
    exclude '*.proto'
    includeEmptyDirs false
}

tasks.named("sourcesJar") {
    // The 'dependsOn generateProto' configuration avoids gradle warning:  Task ':proto:sourcesJar' uses
    // this output of task ':proto:generateProto' without declaring an explicit or implicit dependency.
    dependsOn generateProto
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = project.getGroup()
            artifactId = project.getName()
            version = project.getVersion()
            from components.java
        }
    }
    // Publish to jitpack:         $ ./gradlew clean build assemble publish
    // Install in local mvn repo:  $ ./gradlew clean build publishToMavenLocal
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/ghubstan/" + project.getName())
            credentials {
                username = "ghubstan"
                password = System.getProperty("ghubtoken")
            }
        }
    }
}

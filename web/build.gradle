// org.misq.web is an unnamed module because it relies on many un-modularized jars.
// See https://www.logicbig.com/tutorials/core-java-tutorial/modules/unnamed-modules.html
// Summary
//    The unnamed module does not have module-info.java, which means the classes which do not have
//    modules are promoted to the unnamed module.
//    The unnamed module 'requires' all other modules and 'exports' all its packages.
//    The classes which were written in the older versions but now running in Java 9 environment will
//    continue to work because they become the members of the unnamed module.
//    If we want to run old code in Java 9 environment without migrating them to the module system,
//    we should run them using class path (not module path)
//
plugins {
    id 'misq.java-conventions'
    id 'java-library-distribution'
}

distributions {
    main {
        distributionBaseName = 'web-dist'
    }
}

ext { // in alphabetical order
    ratpackVersion = '1.9.0-rc-2'
    ratpackLogbackVersion = '1.1.11'
    ratpackSlf4jVersion = '1.7.30'
}

dependencies {
    implementation project(':third-party:libgson')
    implementation project(':third-party:libguice')
    implementation project(':third-party:libnetty.4.1.63')
    implementation project(':api')

    implementation "io.ratpack:ratpack-core:$ratpackVersion"
    implementation "io.ratpack:ratpack-guice:$ratpackVersion"
    implementation "io.ratpack:ratpack-rx2:$ratpackVersion"

    implementation "org.slf4j:slf4j-api:$ratpackSlf4jVersion"
    implementation "ch.qos.logback:logback-core:$ratpackLogbackVersion"
    implementation("ch.qos.logback:logback-classic:$ratpackLogbackVersion") {
        exclude(module: 'javax.mail')
    }

    compileOnly "org.projectlombok:lombok:$lombokVersion"
    compileOnly "javax.annotation:javax.annotation-api:$javaxAnnotationVersion"
    annotationProcessor "org.projectlombok:lombok:$lombokVersion"
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = project.getGroup()
            artifactId = project.getName()
            version = project.getVersion()
            from components.java
        }
    }
    // Publish to jitpack:         $ ./gradlew clean build assemble publish
    // Install in local mvn repo:  $ ./gradlew clean build publishToMavenLocal
    //
    // To import individual module-playground modules:
    //    implementation 'com.github.ghubstan.module-playground:cli:<commit>'
    //    implementation 'com.github.ghubstan.module-playground:fisq:<commit>'
    // This does import the transitive dependencies.
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/ghubstan/" + project.getName())
            credentials {
                username = "ghubstan"
                password = System.getProperty("ghubtoken")
            }
        }
    }
}

signing {
    // See https://docs.gradle.org/current/userguide/signing_plugin.html#signing_plugin
    // The Signing Plugin currently only provides support for generating OpenPGP signatures
    // (which is the signature format required for publication to the Maven Central Repository).
    //
    // sign publishing.publications.maven
    // Cannot perform signing task ':cli:signMavenPublication' because it has no configured signatory
}
